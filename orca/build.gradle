/*
 * Copyright 2014 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  repositories {
    jcenter()
    maven { url "http://dl.bintray.com/spinnaker/gradle" }
  }
  dependencies {
    classpath "com.netflix.spinnaker:spinnaker-gradle-project:1.12.36"
  }
}

ext {
  versions = [
    batch   : "3.0.2.RELEASE",
    groovy  : "2.3.8",
    guava   : "18.0",
    jackson : "2.4.4",
    kork    : "1.25",
    okhttp  : "2.0.0",
    retrofit: "1.6.1",
    rx      : "1.0.2",
    slf4j   : "1.7.8",
    spock   : "0.7-groovy-2.0",
    spring  : "4.0.8.RELEASE"
  ]

  commonDependencies = [
    jacksonDatabind : dependencies.create("com.fasterxml.jackson.core:jackson-databind:$versions.jackson"),
    jacksonGuava    : dependencies.create("com.fasterxml.jackson.datatype:jackson-datatype-guava:$versions.jackson"),
    korkJedis       : dependencies.create("com.netflix.spinnaker.kork:kork-jedis:$versions.kork"),
    korkJedisTest   : dependencies.create("com.netflix.spinnaker.kork:kork-jedis-test:$versions.kork"),
    redbatch        : dependencies.create("com.netflix:redbatch:0.1"),

    spockSpring     : dependencies.create("org.spockframework:spock-spring:$versions.spock"),
    springBatchTest : dependencies.create("org.springframework.batch:spring-batch-test:$versions.batch"),
    springTest      : dependencies.create("org.springframework:spring-test:$versions.spring")
  ]
}

allprojects {
  apply plugin: "spinnaker-gradle-project"
  group = "com.netflix.spinnaker.orca"

  configurations.all {
    exclude group: 'javax.servlet', module: 'servlet-api'
    exclude group: "org.slf4j", module: "slf4j-log4j12"
    exclude group: "org.slf4j", module: "slf4j-simple"
    resolutionStrategy {
      eachDependency {
        if (it.requested.group == 'com.netflix.rxjava' && it.requested.name == 'rxjava-core') {
          it.useTarget group: 'io.reactivex', name: 'rxjava', version: '1.0.2'
        }
      }
    }
  }
}
subprojects {
  apply plugin: 'groovy'
  plugins.withType(JavaPlugin) {
    sourceCompatibility = "1.7"
    targetCompatibility = "1.7"
  }
  dependencies {
    spinnaker.group('spockBase')
    compile spinnaker.dependency('groovy')
    compile spinnaker.dependency('guava')
    compile spinnaker.dependency('slf4jApi')
    testRuntime spinnaker.dependency('slf4jSimple')
  }
}

defaultTasks "build"
tasks.checkSnapshotDependencies.enabled = false
