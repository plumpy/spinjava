/*
 * Copyright 2014 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'spring-boot'
apply plugin: 'spinnaker-debpublish'

ext {
  springConfigLocation = System.getProperty('spring.config.location', "${System.getProperty('user.home')}/.spinnaker/")
}

tasks.withType(org.springframework.boot.gradle.run.BootRunTask) {
  systemProperty('spring.config.location', project.springConfigLocation)
}

springBoot {
    mainClass = 'com.netflix.spinnaker.orca.Main'
}

dependencies {
  spinnaker.group("bootWeb")

  compile project(":orca-bakery")
  compile project(":orca-clouddriver")
  compile project(":orca-front50")
  compile project(":orca-applications")
  compile project(":orca-flex")
  compile project(":orca-echo")
  compile project(":orca-mine")
  compile project(":orca-rush")
  compile project(":orca-igor")
  compile project(":orca-eureka")
  compile project(":orca-tide")

  runtime spinnaker.dependency("kork")
  compile spinnaker.dependency("korkWeb")

  spinnaker.group('test')
  testCompile spinnaker.dependency('batchTest')
  testCompile project(":orca-test")
}

applicationName = 'orca'
applicationDefaultJvmArgs = ["-Djava.security.egd=file:/dev/./urandom"]
applicationDistribution.from(project.file('config')) {
  into "config"
}

sourceSets {
  main {
    resources {
      srcDir 'src/main/resources'
      srcDir 'config'
    }
  }
}

jar {
  doFirst {
    exclude "${rootProject.name}.yml"
  }
}

startScripts {
  doLast {
    unixScript.text = unixScript.text.replace('CLASSPATH=$APP_HOME', 'CLASSPATH=$APP_HOME/config:$APP_HOME')
    windowsScript.text = windowsScript.text.replace('set CLASSPATH=', 'set CLASSPATH=%APP_HOME%\\config;')
  }
}

ospackage {
  packageName = project.applicationName
  version = project.version.replaceAll("-SNAPSHOT", "")
  release '3'
  into "/opt/${project.applicationName}"
  from "${project.buildDir}/install/${project.applicationName}"
}

buildDeb {
  dependsOn installApp
  requires("openjdk-8-jre")
  requires("redis-server")
}
/*
applicationDefaultJvmArgs = [
  "-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5005"
]
*/
