buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:1.0.2.RELEASE"
  }
}

apply plugin: 'base'
apply plugin: 'spring-boot'
apply plugin: 'idea'

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
}

ext {
  internal = false
}

try {
  def inet = new InetSocketAddress("artifacts.netflix.com", 80)
  new Socket().connect(inet, 5)
  internal = true
  repositories.maven { url "http://artifacts.netflix.com/gradle-netflix-local" }
} catch (ignore) {}

/**
 * This is mostly all because of logging.
 */
configurations.all {
  resolutionStrategy {
    force 'org.apache.log4j:log4j:1.2.17'
    force 'com.google.guava:guava:14.0.1'
    force 'commons-codec:commons-codec:1.7'
  }
  exclude group: 'javax.servlet', module: 'servlet-api'
  exclude group: 'org.mortbay.jetty', module: 'servlet-api'
  exclude group: 'ch.qos.logback', module: 'logback-classic'
  exclude group: 'org.slf4j', module: 'log4j-over-slf4j'
  exclude group: 'commons-logging', module: 'commons-logging'
}

dependencies {
  if (internal) {
    compile "com.netflix.asgard.kato:kato-perforce-udp:1.3-SNAPSHOT"
  }

  // Web module, which provides the REST endpoints.
  compile project(":kato-web")
  // AWS deployment module
  compile project(":kato-aws")
  // Google Compute deployment module
  compile project(":kato-gce")
}

// "gradle bootRun" will fire this up.
run {
  systemProperties = System.properties
  main = 'com.netflix.asgard.kato.Main'
}
