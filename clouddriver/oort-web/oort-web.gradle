buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    classpath "com.netflix.nebula:gradle-ospackage-plugin:1.12.2"
  }
}

apply plugin: 'os-package'
apply plugin: 'spring-boot'

configurations.all {
  exclude group: 'javax.servlet', module: 'servlet-api'
  exclude group: "org.slf4j", module: "slf4j-log4j12"
}

ideaConfig {
  appClassName = 'com.netflix.spinnaker.oort.Main'
}

dependencies {
  compile project(":oort-core")
  compile project(":oort-aws")
  compile project(":oort-gce")
  spinnaker.group("bootWeb")

  compile spinnaker.dependency('eurekaClient')
}

applicationName = 'oort'
applicationDefaultJvmArgs = ["-Djava.security.egd=file:/dev/./urandom"]

sourceSets {
  main {
    resources {
      srcDir 'src/main/resources'
      srcDir 'config'
    }
  }
}

ospackage {
  packageName = project.applicationName
  version = project.version.replaceAll("-SNAPSHOT", "")
  release '3'
  into "/opt/${project.applicationName}"
  from "${project.buildDir}/install/${project.applicationName}"
}

buildDeb {
  dependsOn installApp
  requires("openjdk-7-jre")
  requires("redis-server")
}

