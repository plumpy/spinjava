buildscript {
  ext {
    springBootVersion = "1.1.7.RELEASE"
  }
  repositories {
    jcenter()
  }
  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    classpath 'com.netflix.nebula:gradle-ospackage-plugin:1.12.+'
  }
}

apply plugin: 'spring-boot'
apply plugin: 'os-package'
apply plugin: 'groovy'
apply plugin: 'idea'

repositories {
  jcenter()
  maven { url "http://repo.spring.io/snapshot" }
}

dependencies {
  compile project(":gate-manual")

  compile spinnaker.dependency("groovy")
  compile spinnaker.dependency("kork")
  compile spinnaker.dependency("frigga")

  compile 'org.springframework:springloaded:1.2.1.RELEASE'

  compile 'cglib:cglib-nodep:3.1'
  compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
  compile 'com.netflix.hystrix:hystrix-core:1.4.0-RC5'
  compile 'com.squareup.retrofit:retrofit:1.6.1'
  compile 'com.squareup.retrofit:converter-jackson:1.6.1'
  compile "org.springframework.boot:spring-boot-starter-security:${springBootVersion}"

  spinnaker.group("bootWeb")
  spinnaker.group("test")
}

configurations.all {
  exclude group: 'javax.servlet', module: 'servlet-api'
  exclude group: "org.slf4j", module: "slf4j-log4j12"
}

ideaConfig {
  mainClassName = 'com.netflix.spinnaker.gate.Main'
}

tasks.startScripts {
  doLast {
    def scriptFile = file "${outputDir}/${applicationName}"
    scriptFile.text = scriptFile.text.replace('CLASSPATH=$APP_HOME/lib', 'CLASSPATH=$APP_HOME/lib/int/*:$APP_HOME/lib')
  }
}

ospackage {
  packageName = "gate"
  version = project.version.replaceAll("-SNAPSHOT", "")
  release '3'
  into "/opt/${rootProject.name}"
  from "${project.buildDir}/install/${project.name}"
}
buildDeb {
  dependsOn installApp
  requires("openjdk-7-jre")
}
tasks.build.dependsOn = [buildDeb]

tasks.bootRepackage.enabled = false

applicationDefaultJvmArgs = ["-Dserver.port=8084", "-Dspring.config.location=classpath:/application.properties", 
                             "-Dkato.baseUrl=http://localhost/kato", "-Dfront50.baseUrl=http://localhost/front50", 
                             "-Doort.baseUrl=http://localhost/oort", "-Dmort.baseUrl=http://localhost/mort", 
                             "-Dorca.baseUrl=http://localhost/orca", "-Dmayo.baseUrl=http://localhost/mayo", 
                             "-Decho.baseUrl=http://localhost/echo", "-Dflapjack.baseUrl=http://localhost/flapjack"]
