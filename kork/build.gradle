/*
 * Copyright 2014 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  repositories {
    if( project.hasProperty('internalArtifactory') ){
      maven { url project.internalArtifactory }
    } else {
      jcenter()
    }
    maven { url 'http://dl.bintray.com/spinnaker/gradle/' }
  }
  dependencies {
    classpath 'com.netflix.spinnaker:spinnaker-gradle-project:1.9.3'
  }
}

subprojects {
  
  repositories {
    if( parent.hasProperty('internalArtifactory') ){
      maven { url parent.internalArtifactory }
    } else {
      jcenter()
    }
  }

  group = 'com.netflix.spinnaker.kork'
  apply plugin: 'spinnaker-gradle-project'
  apply plugin: 'groovy'
  apply plugin: 'jacoco'

  sourceCompatibility = 1.7
  targetCompatibility = 1.7
  jacoco {
    toolVersion = '0.7.0.201403182114'
  }
  configurations {
    all {
      resolutionStrategy {
        //jcenter seems to be missing the jar for 0.5.12:
        force 'com.netflix.archaius:archaius-core:0.6.0'
      }
    }
  } 
}

project(':kork-core') {
  dependencies {
    compile 'org.springframework.boot:spring-boot-autoconfigure:1.1.0.RELEASE'
    compile 'com.netflix.eureka:eureka-client:1.1.134'
    compile 'com.netflix.astyanax:astyanax-cassandra:1.56.48'
    compile 'com.netflix.astyanax:astyanax-thrift:1.56.48'
    compile 'com.amazonaws:aws-java-sdk:1.7.12'
    testCompile 'org.codehaus.groovy:groovy-all:2.3.2'
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    testCompile 'junit:junit:4.11'
    testRuntime 'org.slf4j:slf4j-simple:1.7.7'
  }
}

